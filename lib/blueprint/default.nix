/* @ts: import type { Lib } from "../type.nix.d.ts"; */
# @ts: { lib: Lib }
{ lib }:

# bruh i'm not using `evalModules` here
let
  newUser =
    { userName # e.g. "ysun"
    , fullName # e.g. "Yifei Sun"
    , profilePicture ? null
    , wallpapersDir ? null
    , keys ? [ ] # e.g. [ "ssh-ed25519 ..." ]
    }: {
      name = userName;
      description = fullName;
      openssh.authorizedKeys.keys = keys;

      meta = { inherit profilePicture wallpapersDir; };
    };

  newHost =
    { hostName # e.g. "bachtel"
    , platform # e.g. "x86_64-linux"
    , os # e.g. "darwin" or "nixos"
    , provider # e.g. "aws", "hetzner", "vultr" lowercase tag for colmena deployment filtering
    , providerName # e.g. "AWS", "Hetzner", "Vultr" display name for DNS comments
    , type # e.g. "laptop", "desktop", "server"
    , domain ? "sd.ysun.co"
    , tags ? [ ]
    , interface ? null # e.g. "eth0", "enp1s0" primary outbound network interface
    , ipv4 ? null
    , ipv6 ? null
    , name ? hostName # e.g. "Butte" display name, falls back to hostName
    , meta ? { } # e.g. { country, region, city, postal, continent }
    , ipam ? { }
    , ranet ? { }
    , services ? { }
    }: {
      inherit platform os provider providerName type; # metadata
      inherit hostName name domain interface ipv4 ipv6 ipam ranet meta; # networking
      inherit services;
      fqdn = "${hostName}.${domain}";
      tags = lib.unique ([ os platform provider type ] ++ tags);
    };
in
{
  # check the yubikeys
  ssh.ca = "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAINVmkIESgegjH4mEwir2mWxZelyAM1wZcbqsn4xGN6l7";

  # provider interface ip addr @ <hostname>.if.ysun.co
  provider.domain = "if.ysun.co";

  # address space we own or occupy (everything under here is trusted)
  net = {
    ipam = {
      ipv4 = [ "23.161.104.0/24" "192.104.136.0/24" ];
      ipv6 = [ "2602:f590::/36" ];
    };

    gravity = {
      ipv4 = [ ];
      ipv6 = [ "2a0c:b641:69c::/48" ];
    };

    tailscale = {
      ipv4 = [ "100.64.0.0/10" ];
      ipv6 = [ "fd7a:115c:a1e0::/48" ];
    };
  };

  ranet = {
    organization = "ysun";
    publicKey = ''
      -----BEGIN PUBLIC KEY-----
      MCowBQYDK2VwAyEADThQqitYOEGZgDk+S2Y9ZcLJVozx3hEOdyjpdK7NOY0=
      -----END PUBLIC KEY-----
    '';
    port = 13000;
  };

  tailscale = {
    tailnet = "tail650e82.ts.net";
    domain = "ts.ysun.co";
    zone = "ysun.co";
    prefix = "co_ysun_ts";
  };

  users = lib.loadAll {
    dir = ./users;
    args = { inherit newUser lib; };
  };

  hosts = lib.loadAll {
    dir = ./hosts;
    args = { inherit newHost lib; };
  };

  services = {
    cache.domain = "cache.ysun.co";
    glance.domain = "home.ysun.co";
    go-csp-collector.domain = "report.ysun.co";
    grafana.domain = "otel.ysun.co";
    home-assistant.domain = "ha.ysun.co";
    jitsi.domain = "meet.ysun.co";
    kanidm.domain = "sso.ysun.co";
    kavita.domain = "read.ysun.co";
    neogrok.domain = "grep.ysun.co";
    niks3.domain = "api.cache.ysun.co";
    ntpd-rs.domain = "time.ysun.co";
    paperless.domain = "dms.ysun.co";
    plausible.domain = "stats.ysun.co";
    vaultwarden.domain = "vault.ysun.co";
  };

  prefixes = {
    experimental = {
      ipv4 = [ ];
      ipv6 = lib.map
        (prefix: {
          inherit prefix;
          option = lib.trim ''
            reject {
                bgp_path.prepend(18932);
              }
          '';
        })
        [
          "2602:f590:a::/48"
          "2602:f590:b::/48"
          "2602:f590:c::/48"
          "2602:f590:d::/48"
          "2602:f590:e::/48"
          "2602:f590:f::/48"
        ];
    };
  };
}
