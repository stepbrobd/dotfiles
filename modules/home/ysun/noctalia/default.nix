{ lib, inputs, ... }:

{ config
, pkgs
, osConfig ? { networking.hostName = ""; }
, ...
}:

let
  cfg = config.programs.noctalia;

  hasTag = lib.hasTag osConfig.networking.hostName;
in
{
  # home-manager added programs.noctalia since 2026-08
  disabledModules = [ "programs/noctalia.nix" ];
  imports = [ inputs.noctalia.homeModules.default ];

  config = lib.mkMerge [
    (lib.mkIf (hasTag "noctalia") {
      programs.noctalia.enable = lib.mkDefault true;
    })

    (lib.mkIf cfg.enable {
      home.packages = with pkgs; [
        brightnessctl
        cliphist
        ddcutil
        wl-clipboard
        wireplumber
      ];

      programs.noctalia.settings = {
        audio.enable_overdrive = true;

        bar = {
          order = [ "default" ];
          default = {
            position = "top";
            background_opacity = 0.75;
            scale = 1.25;
            radius = 12;
            margin_edge = 8;
            margin_ends = 8;
            padding = 16;
            widget_spacing = 16;

            start = [ "workspaces" ];
            center = [ ];
            end = [
              "cpu"
              "ram"
              "network_tx"
              "network_rx"
              "battery"
              "control-center"
              "clock"
              "notifications"
            ];
          };
        };

        backdrop = {
          enabled = true;
          blur_intensity = 0.0;
          tint_intensity = 0.0;
        };

        brightness.enable_ddcutil = true;

        control_center.shortcuts = [
          { type = "wifi"; }
          { type = "bluetooth"; }
          { type = "wallpaper"; }
          { type = "notification"; }
          { type = "caffeine"; }
          { type = "nightlight"; }
        ];

        dock.enabled = false;

        idle = {
          pre_action_fade_seconds = 5.0;
          behavior = {
            lock = {
              action = "lock";
              enabled = true;
              timeout = 300;
            };
            screen-off = {
              action = "screen_off";
              enabled = true;
              timeout = 3600;
            };
          };
        };

        location.auto_locate = true;

        lockscreen = {
          enabled = true;
          fingerprint = true;
          blur_intensity = 0.0;
          tint_intensity = 0.0;
        };

        lockscreen_widgets = {
          enabled = true;
          # spam config so that all monitor outputs have a clock widget
          widget = lib.listToAttrs (lib.map
            (output: lib.nameValuePair "clock@${output}" {
              type = "clock";
              inherit output;
              cx = 960.0;
              cy = 256.0;
              placement_width = 1920.0;
              placement_height = 1280.0;
              box_width = 512.0;
              box_height = 256.0;
              settings = {
                clock_style = "digital";
                format = "{:%H:%M}";
                color = "error";
                shadow = false;
                background = false;
              };
            })
            ([ "eDP-1" ]
              ++ lib.map (i: "DP-${lib.toString i}") (lib.range 1 8) # bc DP can be multiplexed
              /* ++ lib.map (i: "HDMI-A-${lib.toString i}") (lib.range 1 4) */));
        };

        notification.position = "top_right";

        osd.position = "top_right";

        shell = {
          avatar_path = lib.blueprint.users.ysun.meta.profilePicture;
          clipboard_auto_paste = "off";
          greeter_sync.auto_sync = true;
          password_style = "random";
          launcher = {
            categories = true;
            sort_by_usage = true;
            providers.session.global = true;
          };
          screenshot.directory = "${config.home.homeDirectory}/Pictures/Screenshots";
        };

        theme = {
          mode = "dark";
          source = "builtin";
          builtin = "Nord";
        };

        wallpaper = {
          enabled = true;
          directory = lib.blueprint.users.ysun.meta.wallpapersDir;
          fill_mode = "crop";
          transition_duration = 1500;
          automation = {
            enabled = true;
            interval_seconds = 3600;
            order = "random";
          };
        };

        weather = {
          enabled = true;
          unit = "metric";
          effects = false;
        };

        widget = {
          workspaces.hide_when_empty = true;

          clock = {
            type = "clock";
            format = "{:%a %b %-d %H:%M:%S}";
          };

          control-center = {
            type = "control-center";
            glyph = "adjustments";
          };

          cpu = {
            type = "sysmon";
            stat = "cpu_usage";
            show_value = false;
          };
          ram = {
            type = "sysmon";
            stat = "ram_used";
            show_value = false;
          };
          network_rx = {
            type = "sysmon";
            stat = "net_rx";
            show_value = false;
          };
          network_tx = {
            type = "sysmon";
            stat = "net_tx";
            show_value = false;
          };
        };
      };
    })
  ];
}
