{ lib, config, ... }:

let
  cfg = config.services.as10779;
in
{
  sops.secrets.bgp = {
    sopsFile = ./secrets.yaml;
    mode = "440";
    owner = config.systemd.services.bird.serviceConfig.User;
    group = config.systemd.services.bird.serviceConfig.Group;
    reloadUnits = [ config.systemd.services.bird.name ];
  };

  services.as10779 = {
    enable = true;

    local = {
      hostname = config.networking.hostName;
      interface = {
        local = lib.blueprint.hosts.toompea.ipam.interface;
        primary = lib.blueprint.hosts.toompea.interface;
      };
      ipv4.addresses = [
        "${lib.blueprint.hosts.toompea.ipam.ipv4}/32" # unicast
        "23.161.104.17/32" # personal site anycast
        # "44.32.189.0/24" # 44net anycast test
      ];
      ipv6.addresses = [
        "${lib.blueprint.hosts.toompea.ipam.ipv6}/128" # unicast
        "2602:f590::23:161:104:17/128" # personal site anycast
      ];
    };

    router = {
      secret = config.sops.secrets.bgp.path;
      source = { inherit (lib.blueprint.hosts.toompea) ipv4 ipv6; };
      static =
        let
          # option = "reject";
          # 3204:XXXx per whois AS3204 remarks, x = 1-3 prepend Nx, x = 0 dont announce
          # 100x Telia, 110x xTom, 170x Liberty Global, 190x Tata, others are IXes
          # telia and tata haul their whole footprint to a customer route
          # if needed (3204, 1101) prepend 1x toward xTom to shrink the IX pull
          option = lib.trim ''
            reject {
                bgp_community.add((3204, 1000)); # dont announce to Telia
                bgp_community.add((3204, 1900)); # dont announce to Tata
              }
          '';
        in
        {
          ipv4.routes = [
            { inherit option; prefix = "23.161.104.0/24"; }
            # { inherit option; prefix = "44.32.189.0/24"; } # stop announcing 44net for now
            { inherit option; prefix = "192.104.136.0/24"; }
          ];
          ipv6.routes = [
            { inherit option; prefix = "2602:f590::/36"; }
          ] ++ lib.blueprint.prefixes.experimental.ipv6;
        };
      # upstream bgp gateway differs from main interface gateway
      outboundGateway = {
        ipv4 = "185.194.53.4";
        ipv6 = "2a04:6f00:4::4";
      };
      sessions = [
        {
          name = "xtom";
          password = "PASS_AS3204";
          type = { ipv4 = "direct"; ipv6 = "direct"; };
          neighbor = {
            asn = 3204;
            ipv4 = "185.194.53.4";
            ipv6 = "2a04:6f00:4::4";
          };
          import = {
            ipv4 = "import all;";
            ipv6 = "import all;";
            # ipv4 = "import filter ${cfg.router.rpki.ipv4.filter};";
            # ipv6 = "import filter ${cfg.router.rpki.ipv6.filter};";
          };
          export = {
            ipv4 = ''export where proto = "${cfg.router.static.ipv4.name}";'';
            ipv6 = ''export where proto = "${cfg.router.static.ipv6.name}";'';
          };
        }
        {
          name = "bgptools";
          password = null;
          type = { ipv4 = "disabled"; ipv6 = "multihop"; };
          mp = "v4 over v6";
          neighbor = {
            asn = 212232;
            ipv4 = null;
            ipv6 = "2a0c:2f07:9459::b19";
          };
          import = {
            ipv4 = "import none;";
            ipv6 = "import none;";
          };
          export = {
            ipv4 = "export all;";
            ipv6 = "export all;";
          };
        }
      ];
    };
  };
}
